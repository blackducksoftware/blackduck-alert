buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE'
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.11'
        classpath 'org.liquibase:liquibase-gradle-plugin:1.1.1'
        classpath 'com.h2database:h2:1.4.196'
    }
}

plugins {
    id 'org.springframework.boot' version '1.5.8.RELEASE'
    id 'application'
    id 'com.moowork.node' version '1.2.0'
}

version = '0.0.1-SNAPSHOT'
def dbChangeLogVersion = '1.0.0'

apply plugin: 'com.blackducksoftware.integration.solution'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'org.liquibase.gradle'

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

mainClassName = 'com.blackducksoftware.integration.hub.alert.Application'
springBoot { mainClass = mainClassName }

applicationDefaultJvmArgs = [
    '-DAPP_HOME=APP_HOME_VAR_TOKEN'
]

dependencies {
    compile 'com.blackducksoftware.integration:hub-common:26.1.0'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-batch'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework:spring-webmvc:4.3.7.RELEASE'
    compile 'org.springframework.security:spring-security-web:4.2.3.RELEASE'
    compile 'org.springframework.security:spring-security-config:4.2.3.RELEASE'
    compile 'org.liquibase:liquibase-core'
    runtime 'com.h2database:h2'

    compile 'org.jsoup:jsoup:1.9.2'
    compile 'javax.mail:mail:1.4.7'
    compile 'org.freemarker:freemarker:2.3.25-incubating'

    compile 'org.springframework.boot:spring-boot-starter-activemq'
    compile 'org.apache.activemq:activemq-broker'
    compile 'org.apache.activemq:activemq-kahadb-store:5.14.5'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.0'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.0'
    compile 'javax.interceptor:javax.interceptor-api:1.2.1'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'com.github.springtestdbunit:spring-test-dbunit:1.3.0'
    testCompile 'org.springframework.security:spring-security-test:4.0.0.RELEASE'
}

jar {
    sourceSets {
        main {
            resources {
            }
        }
    }
}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace("\"\$APP_ARGS\"", "\"\$APP_ARGS\" --spring.config.location=file:/blackduck-alert-config-volume ")
        windowsScript.text = windowsScript.text.replace("\"%APP_ARGS%\"", "\"%APP_ARGS%\" --spring.config.location=file:/blackduck-alert-config-volume ")
        unixScript.text = unixScript.text.replace("APP_HOME_VAR_TOKEN", "\$APP_HOME")
        windowsScript.text = windowsScript.text.replace("APP_HOME_VAR_TOKEN", "%APP_HOME%")
    }
}

distributions {
    main {
        contents {
            from('src/main/resources/alert.sh') {into 'bin/'}
            from('src/main/resources/application.properties') {into '/blackduck-alert-config-defaults/'}
            from('src/main/resources/email/images/') {into 'images/'}
            from('src/main/resources/email/templates/') {into 'templates/'}
        }
    }
}

liquibase {
    activities {
        main {
            changeLogFile "$projectDir/src/main/resources/db/changelog/alert/changelog-${dbChangeLogVersion}.xml"
            url 'jdbc:h2:file:./data/alertdb'
            username 'sa'
            defaultSchemaName 'alert'
        }
    }
}

task cleanBundles(type: Delete) {
    File staticFolder = new File('src/main/resources/static')
    if (staticFolder.exists()) {
        staticFolder.deleteDir()
    }
}

tasks.clean.finalizedBy(cleanBundles)

npm_run() {
    args = ['build']
}

task webpack(type: Exec) {
        inputs.file("package-lock.json")
        inputs.file("webpack.config.js")
        inputs.dir("$projectDir/src/main")

        outputs.dir("$projectDir/src/main/resources/static/built/bundle.js")

        commandLine "$projectDir/node_modules/.bin/webpack", "$projectDir/src/main/js/App.js", "$projectDir/src/main/resources/static/built/bundle.js"
}

tasks.compileJava.finalizedBy(npm_run)

task runServer(type: Exec, dependsOn: [build]) {
    File jarFile = new File("build/libs/hub-alert-${version}.jar")
    commandLine 'java', '-jar', jarFile.getAbsolutePath()
}

task stageDocker(type:Copy, dependsOn: [build]) {
    into "${project.buildDir}/docker"
    from("${project.projectDir}") {
        include 'Dockerfile'
        include 'docker-entrypoint.sh'
    }
    into('build/distributions') {
        from distTar
    }
}

task buildDockerImage(type:DockerBuildImage, dependsOn: [stageDocker]) {
    inputDir = file("${project.buildDir}/docker")
    tag = "blackducksoftware/${project.name}:${project.version}"
    buildArgs = [ 'VERSION': "${project.version}" ]
}

task dockerLogin(type: Exec) {
    commandLine "docker", "login", "--username", "$System.env.DOCKER_HUB_USER", "--password", "$System.env.DOCKER_HUB_PASSWORD"
}

task pushImage(type:Exec, dependsOn: [buildDockerImage]) {
    commandLine "docker", "push", "blackducksoftware/${project.name}:${project.version}"
}

task all(dependsOn: [build, dockerLogin, buildDockerImage, pushImage]) {
    dockerLogin.mustRunAfter build
    buildDockerImage.mustRunAfter dockerLogin
    pushImage.mustRunAfter buildDockerImage    
}

def tasksPackages = [
    'testIntegration' : 'com.blackducksoftware.integration.IntegrationTest',
    'testDatabaseConnection' : 'com.blackducksoftware.integration.DatabaseConnectionTest',
    'testExternalConncetion' : 'com.blackducksoftware.integration.ExternalConnectionTest',
    'testHubConnection' : 'com.blackducksoftware.integration.HubConnectionTest',
    'testPerformance' : 'com.blackducksoftware.integration.PerformanceTest',
    'testTurtle' : 'com.blackducksoftware.integration.TurtleTest',
    'testUnique' : 'com.blackducksoftware.integration.UniqueTest'
]
    
test { 
    useJUnit { 
        excludeCategories = tasksPackages.values()
    } 
}

tasksPackages.each { taskName, packageName ->
    task "$taskName"(type: Test) {
        useJUnit { includeCategories packageName }
    }
}
