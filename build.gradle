buildscript {
    ext {
        springBootVersion = '3.4.13'
    }

    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-repositories.gradle', to: buildscript

    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-cgp-version.gradle'

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.blackduck.integration:common-gradle-plugin:${managedCgpVersion}"
    }
}

plugins {
    id 'application'
    id 'com.dorongold.task-tree' version '2.1.1'
}

ext {
    userID = 'id -u'.execute().text.trim()
    groupID = 'id -g'.execute().text.trim()

    commitHash = 'UNKNOWN'

    // Copied from src/test/java - com.blackduck.integration.alert.TestTags
    junitPlatformCustomTestTags = 'BlackDuckConnection, DatabaseConnection, ExternalConnection, Swagger'

    javaSourceCompatibility = JavaVersion.VERSION_17
    javaTargetCompatibility = JavaVersion.VERSION_17

    moduleName = 'com.blackduck.integration.alert.main'

    // This version is also in src/test/resources/spring-test.properties for the test jdbc connection
    postgresContainerVersionMigration = '15.8'
    postgresContainerVersionCurrent = '16.11'

    // All variables that start with 'dockerBuildParam+' will all be passed to the 'docker build' command
    dockerBuildParam_BASEIMAGE_HUB_DOCKER_COMMON = 'blackducksoftware/hub-docker-common:1.0.7'
    dockerBuildParam_BASEIMAGE_BLACKDUCK_ALERT = 'alpine:3.20'
    dockerBuildParam_BASEIMAGE_BLACKDUCK_ALERT_DB_MIGRATION = 'postgres:' + postgresContainerVersionMigration + '-alpine'
    dockerBuildParam_BASEIMAGE_BLACKDUCK_ALERT_DB = 'postgres:' + postgresContainerVersionCurrent + '-alpine'
    dockerBuildParam_POSTGRES_MIGRATION_VERSION = postgresContainerVersionMigration
    dockerBuildParam_BASEIMAGE_BLACKDUCK_ALERT_RABBITMQ = 'rabbitmq:3.13-alpine'

    // General Docker Variables
    dockerStagingDirectory = project.buildDir.toString() + '/docker-staging'
    baseDockerDirectory = project.projectDir.toString() + '/docker'
    dockerCleanStagingAreaDirectoryStageName = 'dockerCleanStagingAreaDirectory'
    dockerRemoveAllImagesStageName = 'dockerRemoveAllImages'
    dockerSetupStagingAreaDirectoryStageName = 'dockerSetupStagingAreaDirectory'
    dockerBuildAllImagesStageName = 'dockerBuildAllImages'
    dockerPublishAllImagesStageName = 'dockerPublishAllImages'
    dockerPublishAllImages_DHStageName = 'dockerPublishAllImages_DH'
    dockerPublishAllImages_ARTStageName = 'dockerPublishAllImages_ART'

    helmArtifactoryRepos = []
    helmPrePackagedRepoNames = []

    envVarsNotExistMsg = 'Skipping as dependent variable are not set or values are false'
}

mainClassName = 'com.blackduck.integration.alert.Application'
version = '9.0.0-SNAPSHOT'

ext.isSnapshot = project.version.endsWith('-SNAPSHOT')
ext.isSIGQA = project.version.contains('-SIGQA')
ext.isRelease = (!project.ext.isSnapshot && !project.ext.isSIGQA)

if (project.ext.isSIGQA && System.env.HELM_SIGQA_REPOS != null) {
    helmArtifactoryRepos = System.env.HELM_SIGQA_REPOS.split(',').collect { it.trim() as String }
} else if (project.ext.isSnapshot && System.env.HELM_SNAPSHOT_REPOS != null) {
    helmArtifactoryRepos = System.env.HELM_SNAPSHOT_REPOS.split(',').collect { it.trim() as String }
} else if (project.ext.isRelease && System.env.HELM_RELEASE_REPOS != null) {
    helmArtifactoryRepos = System.env.HELM_RELEASE_REPOS.split(',').collect { it.trim() as String }
} else {
    logger.lifecycle("Helm Artifactory repositores not configured")
}

if (System.env.HELM_PREPACKAGED_REPOS != null) {
    helmPrePackagedRepoNames = System.env.HELM_PREPACKAGED_REPOS.split(',').collect { it.trim() as String }
}

apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.blackduck.integration.solution'

apply from: 'buildSrc/buildTasks.gradle'
apply from: 'buildSrc/runTasks.gradle'
apply from: 'buildSrc/deploymentTasks.gradle'
apply from: 'buildSrc/docker.gradle'
apply from: 'buildSrc/postman.gradle'

subprojects {
    if (project.name != 'alert-platform') {
        version = rootProject.version
        apply plugin: 'com.blackduck.integration.simple'

        dependencies {
            implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        }
    }
}

allprojects {
    configurations {
        all {
            exclude group: 'com.blackducksoftware.bdio', module: 'bdio2'
        }
    }
    // disable the test task when running the alert server to speed up startup time.
    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(runServer)) {
            compileTestJava.enabled = false
            test.enabled = false
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-parameters'
    }

    repositories {
        // Need this repository to resolve the opensaml dependencies
        maven {
            url 'https://build.shibboleth.net/nexus/content/repositories/releases/'
        }
    }
}

// configure the version of the JaCoCo plugin to 0.8.2 which works for Java 11 until we switch to a gradle wrapper that includes 0.8.2+ by default.
jacoco {
    toolVersion = "0.8.9"
}

dependencies {
    implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)

    implementation platform(project(':alert-platform'))

    implementation project(':alert-common')
    implementation project(':alert-database')
    implementation project(':alert-database-job')

    implementation project(':api-authentication')
    implementation project(':api-certificates')
    implementation project(':api-common-model')
    implementation project(':api-channel')
    implementation project(':api-descriptor')
    implementation project(':api-distribution')
    implementation project(':api-environment')
    implementation project(':api-event')
    implementation project(':api-oauth')
    implementation project(':api-processor')
    implementation project(':api-provider')
    implementation project(':api-task')

    implementation project(':authentication-ldap')
    implementation project(':authentication-saml')

    implementation project(':channel-azure-boards')
    implementation project(':channel-email')
    implementation project(':channel-jira-cloud')
    implementation project(':channel-jira-server')
    implementation project(':channel-msteams')
    implementation project(':channel-slack')

    implementation project(':component')
    implementation project(':provider-blackduck')
    implementation project(':ui')
    implementation project(':web')
    implementation project(':service-email')

    testImplementation project(':test-common')
    testImplementation project(':test-common-channel')

    implementation 'com.blackduck.integration:blackduck-common'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core'

    // Logging
    runtimeOnly 'ch.qos.logback:logback-classic'
    runtimeOnly 'ch.qos.logback:logback-core'
    // Tomcat messages use these loggers.
    runtimeOnly 'org.slf4j:jcl-over-slf4j'
    runtimeOnly 'org.slf4j:jul-to-slf4j'

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework.data:spring-data-jpa'
    implementation 'org.springframework.batch:spring-batch-core'
    implementation 'org.springframework.security:spring-security-core'
    implementation 'org.springframework.security:spring-security-web'
    implementation 'org.springframework.security:spring-security-config'

    // =============
    // Test and Dev
    // =============
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:rabbitmq'
    testRuntimeOnly 'org.dbunit:dbunit'
    testRuntimeOnly 'com.playtika.testcontainers:embedded-postgresql'
    testRuntimeOnly 'com.playtika.testcontainers:embedded-rabbitmq'
    testRuntimeOnly 'org.springframework.cloud:spring-cloud-starter-bootstrap'

    if (getGradle().getStartParameter().taskNames.contains('runServer')) {
        def containsExternalDbArg = getGradle().getStartParameter().taskNames.contains("--externaldb")
        def containsExternalRabbitMqArg = getGradle().getStartParameter().taskNames.contains("--external-rabbit")

        if (!containsExternalDbArg || !containsExternalRabbitMqArg) {
            runtimeClasspath 'org.springframework.cloud:spring-cloud-starter-bootstrap'
            runtimeClasspath 'org.springframework.boot:spring-boot-devtools'
        }

        if (!containsExternalDbArg) {
            runtimeClasspath 'com.playtika.testcontainers:embedded-postgresql'
        }

        if (!containsExternalRabbitMqArg) {
            runtimeClasspath 'com.playtika.testcontainers:embedded-rabbitmq'
        }
    }

    testImplementation project(':api-channel-issue-tracker')
    testImplementation 'org.springframework.security:spring-security-ldap'
    testImplementation 'jakarta.persistence:jakarta.persistence-api'
    testImplementation 'org.javassist:javassist'
    testImplementation 'jakarta.mail:jakarta.mail-api'
}

bootJar {
    launchScript()
}

distributions {
    boot {
        contents {
            from("$project.buildDir/libs/liquibase") {
                include 'h2*.jar'
                include 'liquibase-core*.jar'
                include 'logback-*.jar'
                include 'slf4j-api*.jar'
                include 'snakeyaml*.jar'
                into 'lib/liquibase'
            }
            from('src/main/resources/db/changelog-master.xml') { into 'upgradeResources' }
            from('src/main/resources/db/changelog/') { into 'upgradeResources/changelog/' }
            from('alert-database/src/main/resources/liquibase/release-locks-changelog.xml') { into 'upgradeResources' }
            from('alert-database/src/main/resources/scripts/init_alert_db.sql') { into 'upgradeResources' }
            from('docker/blackduck-alert-db') {
                into 'upgradeResources'
                include '**/*.sql'
            }
            from('src/main/resources/images/') { into 'images/' }
            mkdir 'email/attachments'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            def artifactName = "${buildDir}/distributions/${project.name}-${project.version}-deployment.zip"
            artifact(artifactName)
        }
    }
}

artifactory {
    publish {
        defaults { publications('mavenJava') }
    }
}

tasks.helmPushChart.mustRunAfter(helmValidation)
tasks.build.mustRunAfter(copyToTemplates)
tasks.runServer.dependsOn copyToTemplates
tasks.test.dependsOn copyToTemplates

task buildAll(dependsOn: [copyToTemplates, build, helmValidation, createDeploymentZip]) {
    helmValidation.mustRunAfter build
    createDeploymentZip.mustRunAfter helmValidation
}

tasks.getByName(ext.dockerBuildAllImagesStageName).mustRunAfter(buildAll)
tasks.getByName(ext.dockerBuildAllImagesStageName).mustRunAfter(tasks.getByName(ext.dockerSetupStagingAreaDirectoryStageName))
tasks.getByName(ext.dockerSetupStagingAreaDirectoryStageName).mustRunAfter(buildAll)
tasks.getByName(ext.dockerPublishAllImages_DHStageName).mustRunAfter(buildAll)